---
page_title: "WarpGate Terraform Provider"
subcategory: ""
description: |-
  Manage resources in WarpGate, a smart SSH and HTTPS bastion, using Terraform.
---

# WarpGate Terraform Provider

The WarpGate provider allows Terraform to manage [WarpGate](https://github.com/Thunderbottom/warpgate) resources. WarpGate is a smart SSH and HTTPS bastion designed to enhance access management for your infrastructure.

This provider enables you to manage users, roles, targets (SSH, HTTP, MySQL, PostgreSQL) in WarpGate using Terraform.

## Example Usage

```hcl
terraform {
  required_providers {
    warpgate = {
      source  = "Thunderbottom/warpgate"
      version = "1.0.0"
    }
  }
}

provider "warpgate" {
  host  = "https://warpgate.example.com"
  token = var.warpgate_token
}

# Create a role
resource "warpgate_role" "developers" {
  name        = "developers"
  description = "Role for development team members"
}

# Create a user
resource "warpgate_user" "eugene" {
  username    = "eugene"
  description = "Eugene - Development Lead"
  
  credential_policy {
    ssh = ["Password", "PublicKey"]
  }
}

# Create an SSH target
resource "warpgate_target" "web_server" {
  name        = "web-server"
  description = "Production web server"
  
  ssh_options {
    host     = "10.0.0.1"
    port     = 22
    username = "admin"
    public_key_auth {}
  }
}

# Assign the role to the user
resource "warpgate_user_role" "eugene_developer" {
  user_id = warpgate_user.eugene.id
  role_id = warpgate_role.developers.id
}

# Assign the role to the target
resource "warpgate_target_role" "developers_web_access" {
  target_id = warpgate_target.web_server.id
  role_id   = warpgate_role.developers.id
}
```

## Authentication

The WarpGate provider offers a way to authenticate with the WarpGate API using a token. This token can be provided in the provider configuration or via the environment variable `WARPGATE_TOKEN`.

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `host` (String) The WarpGate API host URL (e.g., https://warpgate.example.com)

### Optional

- `token` (String, Sensitive) API token for authenticating with WarpGate API
